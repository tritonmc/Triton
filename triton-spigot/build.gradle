plugins {
    id 'java'
    id 'net.minecrell.plugin-yml.bukkit' version '0.5.2'
}

String pluginDescription = project.property("pluginDescription")
String pluginWebsite = project.property("pluginWebsite")
String pluginAuthor = project.property("pluginAuthor")
String pluginApiVersion = project.property("pluginApiVersion")
String pluginName = project.property("pluginName")

apply plugin: 'net.minecrell.plugin-yml.bukkit'
group 'com.rexcantor64.triton'

dependencies {
    compileOnly project(path: ":api")
    compileOnly project(path: ":core")
    implementation project(path: ":spigot-legacy")
    implementation project(path: ":v1_13")

    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'

    compileOnly 'net.kyori:adventure-text-serializer-gson:4.11.0'
    compileOnly 'net.kyori:adventure-text-serializer-legacy:4.11.0'
    compileOnly 'net.kyori:adventure-text-serializer-bungeecord:4.1.2'

    compileOnly 'com.comphenix.protocol:ProtocolLib:5.1.0-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.1'

    // Libraries available on Spigot
    compileOnly 'org.fusesource.jansi:jansi:2.4.0' // Used for terminal translation

    implementation 'org.bstats:bstats-bukkit:3.0.0'
}

bukkit {
    name = pluginName
    version = project.version
    description = pluginDescription
    website = pluginWebsite
    author = pluginAuthor
    main = 'com.rexcantor64.triton.spigot.plugin.SpigotPlugin'
    apiVersion = pluginApiVersion

    depend = ['ProtocolLib']
    softDepend = ['PlaceholderAPI']

    commands {
        twin {
            description = 'Upload and download your config to/from the Triton Web INterface.'
            usage = '/twin [code]'
        }
    }

    permissions {
        'triton.getflag' {
            setDefault('OP')
        }
        'triton.setlanguage' {
            setDefault('TRUE')
        }
        'triton.setlanguage.others' {
            setDefault('OP')
        }
        'triton.openselector' {
            setDefault('TRUE')
        }
        'triton.reload' {
            setDefault('OP')
        }
        'triton.help' {
            setDefault('TRUE')
        }
        'triton.sign' {
            setDefault('OP')
        }
        'triton.database' {
            setDefault('OP')
        }
        'triton.info' {
            setDefault('TRUE')
        }
        'triton.loglevel' {
            setDefault('OP')
        }
        'twin.upload' {
            setDefault('OP')
        }
        'twin.download' {
            setDefault('OP')
        }
    }
}
