plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

apply plugin: 'com.github.johnrengelman.shadow'
group 'com.rexcantor64.triton'

allprojects {
    apply plugin: 'java'
    version '4.0.0-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.dmulloy2.net/nexus/repository/public/' }
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven {
            name 'CodeMC'
            url 'https://repo.codemc.org/repository/maven-public'
        }
        maven {
            name 'velocity'
            url 'https://nexus.velocitypowered.com/repository/maven-public/'
        }
        maven {
            name 'diogotcRepositorySnapshots'
            url 'https://repo.diogotc.com/snapshots/'
        }
        maven {
            // Mirror other people's repositories
            name 'diogotcRepositoryMirror'
            url 'https://repo.diogotc.com/mirror/'
        }
    }
}

subprojects {
    dependencies {
        implementation 'org.jetbrains:annotations:23.0.0'

        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'

        testCompileOnly 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    }
}

dependencies {

    implementation project(path: ":core")
    implementation project(path: ":triton-bungeecord")
    implementation project(path: ":triton-spigot")
    implementation project(path: ":triton-velocity")
}

shadowJar {
    getArchiveBaseName().set(rootProject.name)
    relocate 'org.bstats', 'com.rexcantor64.triton.metrics'
    //relocate 'org.slf4j', 'com.rexcantor64.shaded.slf4j'
    relocate 'net.kyori.adventure.text.minimessage', 'com.rexcantor64.shaded.minimessage'
    relocate 'com.tananaev.jsonpatch', 'com.rexcantor64.shaded.jsonpatch'
    relocate 'com.zaxxer.hikari', 'com.rexcantor64.shaded.hikari'

    relocate 'net.kyori.adventure.text.serializer.gson', 'com.rexcantor64.triton.lib.adventure.serializer.gson'
    relocate 'net.kyori.adventure.text.serializer.legacy', 'com.rexcantor64.triton.lib.adventure.serializer.legacy'
    relocate 'net.kyori.adventure.text.serializer.plain', 'com.rexcantor64.triton.lib.adventure.serializer.plain'
    relocate 'net.kyori.adventure.text.serializer.bungeecord', 'com.rexcantor64.triton.lib.adventure.serializer.bungeecord'

    relocate 'net.byteflux.libby', 'com.rexcantor64.triton.lib.libby'
}
