plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

apply plugin: 'com.github.johnrengelman.shadow'
group 'com.rexcantor64.triton'

allprojects {
    apply plugin: 'java'
    version '4.0.0-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name 'diogotcRepositorySnapshots'
            url 'https://repo.diogotc.com/snapshots/'
        }
        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.dmulloy2.net/nexus/repository/public/' }
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven {
            name 'CodeMC'
            url 'https://repo.codemc.org/repository/maven-public'
        }
        maven {
            name 'velocity'
            url 'https://nexus.velocitypowered.com/repository/maven-public/'
        }
        maven {
            // Mirror other people's repositories
            name 'diogotcRepositoryMirror'
            url 'https://repo.diogotc.com/mirror/'
        }
    }
}

subprojects {
    dependencies {
        compileOnly 'org.jetbrains:annotations:23.0.0'

        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'

        testCompileOnly 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    }
}

dependencies {
    implementation project(':api')
    implementation project(':core:triton-core-loader')
    implementation project(':triton-spigot:triton-spigot-loader')
    implementation project(':triton-velocity:triton-velocity-loader')

    compileOnly project(':core')
    compileOnly project(':triton-bungeecord')
    compileOnly project(':triton-spigot')
    compileOnly project(':triton-velocity')
}

shadowJar {
    getArchiveBaseName().set(rootProject.name)

    from {
        project(':core').tasks.shadowJar.archiveFile
    }
    from {
        project(':triton-spigot').tasks.shadowJar.archiveFile
    }
    from {
        project(':triton-velocity').tasks.shadowJar.archiveFile
    }

    relocate 'org.objectweb.asm', 'com.rexcantor64.triton.lib.objectweb.asm'
    relocate 'me.lucko.jarrelocator', 'com.rexcantor64.triton.lib.jarrelocator'
}
